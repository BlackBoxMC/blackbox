plugins {
    id 'com.github.johnrengelman.shadow' version '7.1.0'
    id 'kr.entree.spigradle' version '2.2.4'
    id 'io.freefair.lombok' version '6.3.0'
    id 'java'
    id 'jacoco'
    id 'idea'
}

group = 'net.ioixd'
version = '1.1'

compileJava {
    options.compilerArgs += ["-h", file("include")]
}

repositories {
    mavenCentral()
    maven {
        url = 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/'

        // As of Gradle 5.1, you can limit this to only those
        // dependencies you expect from it
        content {
        includeGroup 'org.bukkit'
        includeGroup 'org.spigotmc'
        }
    }
    maven {
        name = "sonatype"
        url = "https://oss.sonatype.org/content/groups/public/"
    }
}
dependencies {
    implementation "org.spigotmc:spigot-api:1.20.1-R0.1-SNAPSHOT"
    implementation "io.github.classgraph:classgraph:4.8.160"
    implementation "org.reflections:reflections:0.10.2"
    implementation "io.papermc.paper:paper-api:1.20.1-R0.1-SNAPSHOT"

    implementation "org.slf4j:slf4j-api:2.0.7"
    implementation "org.eclipse.aether:aether-api:1.1.0"
}


shadowJar {
    dependencies {
        include(dependency("io.github.classgraph:classgraph:4.8.160"))
    }
}

tasks.build.dependsOn(shadowJar)
tasks.prepareSpigotPlugins.dependsOn(shadowJar)


def targetJavaVersion = 17
java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
}

tasks.withType(JavaCompile).configureEach {
    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        options.release = targetJavaVersion
    }
}

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('plugin.yml') {
        expand props
    }
}
defaultTasks 'shadowJar'
